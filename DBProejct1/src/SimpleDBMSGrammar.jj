options
{
  static = true;
  DEBUG_PARSER = false;
  IGNORE_CASE = true;
}

PARSER_BEGIN(SimpleDBMSParser)

import db.*;
import java.util.*;

public class SimpleDBMSParser
{
  private static SimpleDBMS dbms = new SimpleDBMS();
  
  public static void main(String args[]) throws ParseException, ErrorException
  {
    SimpleDBMSParser parser = new SimpleDBMSParser(System.in);
    System.out.print("DB_2013-11422> ");

    while (true)
    {
      try
      {
        parser.command();
      }
      catch (ParseException e)
      {
        PrintMessages p = new PrintMessages(Flags.SYNTAX_ERROR);
        p.print();
        SimpleDBMSParser.ReInit(System.in);
      }
      catch (ErrorException e)
      {
        e.p.print();
        SimpleDBMSParser.ReInit(System.in);
      }
    }
  }
}

PARSER_END(SimpleDBMSParser)

SKIP : { " " | "\r" | "\t" | "\n" }

// grammar file의 빨간 글씨로 된 keyword들
TOKEN : /* Keywords */
{
  < EXIT : "exit" >
| < INT : "int" >
| < CHAR : "char" >
| < DATE : "date" >
| < CREATE_TABLE : "create table" >
| < DROP_TABLE : "drop table" >
| < DESC : "desc" >
| < INSERT : "insert into" >
| < DELETE : "delete from" >
| < SELECT : "select" >
| < SHOW_TABLES : "show tables" >
| < NULL : "null" >
| < NOT_NULL : "not null" >
| < PRIMARY_KEY : "primary key" >
| < FOREIGN_KEY : "foreign key" >
| < REFERENCES : "references" >
| < VALUES : "values" >
| < AS : "as" >
| < FROM : "from" >
| < WHERE : "where" >
| < OR : "or" >
| < AND : "and" >
| < NOT : "not" >
| < IS : "is" >
| < ALL : "*" > // '*'가 라벨로 작용할 수 없으므로 임의로 'ALL'을 붙임
}

// 그 외
TOKEN :
{
  < LEGAL_IDENTIFIER : < ALPHABET > (< ALPHABET > | < UNDERSCORE >)* >
| < INT_VALUE : (< SIGN >)? (< DIGIT >)+ >
| < DATE_VALUE : < NNNN > "-" < NN > "-" < NN > >
| < CHAR_STRING : < QUOTE > (< NON_QUOTE_CHARACTER >)* < QUOTE > >
| < SEMICOLON : ";" >
| < LEFT_PAREN : "(" >
| < RIGHT_PAREN : ")" >
| < COMMA : "," >
| < PERIOD : "." >
| < UNDERSCORE : "_" >
| < SIGN : "+" | "-" >
| < DIGIT : [ "0"-"9" ] >
| < ALPHABET : [ "a"-"z", "A"-"Z" ] >
| < COMP_OP : ">" | "<" | "=" | ">=" | "<=" | "!=" >
| < QUOTE : "`" >
| < NNNN : (< DIGIT >){4} >
| < NN : (< DIGIT >){2} >
| < NON_QUOTE_CHARACTER : < DIGIT > | < ALPHABET > | < NON_QUOTE_SPECIAL_CHARACTERS > >
| < NON_QUOTE_SPECIAL_CHARACTERS : [ "~", "`", "!", "@", "#", "$", "%", "^", "&", "*", "(", ")", "-", "_", "+", "=",
"|", "{", "[", "]", "}", "\\", ";", ":", ",", "<", ".", ">", "?", "/"] >
| < UNUSED_QUOTE_MARK : "\'" | "\"" > // 따옴표 실수로 들어갔을 때 syntax error 뱉도록 token에 추가
}

// grammar file 그대로 구현
void command() : {}
{
  queryList()
| (
    < EXIT >
    < SEMICOLON >
    {
      dbms.close();
      System.exit(0);
    }
  ) 
}

void queryList() : { PrintMessages p; }
{
  (
    p = query()
    < SEMICOLON >
    {
      p.print();
    }
  )+
}

PrintMessages query() : { PrintMessages p; }
{
  (
    p = createTableQuery() { return p; }
  | p = dropTableQuery() { return p; }
  | p = descQuery() { return p; }
  | p = insertQuery() { return p; }
  | p = deleteQuery() { return p; }
  | p = selectQuery() { return p; }
  | p = showTablesQuery() { return p; }
  )
  (
    {
      return p;
    }
  )
}

PrintMessages createTableQuery() : { String tn; Table t; ArrayList<Column> cl; }
{
  < CREATE_TABLE >
  tn = tableName()
  {
    if (dbms.isExistingTable(tn)) {
      throw new ErrorException(Flags.TABLE_EXISTENCE_ERROR);
    } else {
      t = dbms.createTable(tn);
    }
  }
  cl = tableElementList(t)
  {
    for (Column c: cl) {
      t.addColumn(c);
    }
    dbms.updateTable(t);
    return new PrintMessages(Flags.CREATE_TABLE_DONE, tn);
  }
}

PrintMessages dropTableQuery() : { String tn; boolean isDeleted; }
{
  < DROP_TABLE >
  tn = tableName()
  {
    if (!dbms.isExistingTable(tn)) {
      throw new ErrorException(Flags.NO_SUCH_TABLE);
    }

    isDeleted = dbms.dropTable(tn);

    if (!isDeleted) {
      throw new ErrorException(Flags.DROP_REFERENCED_TABLE_ERROR, tn);
    }

    return new PrintMessages(Flags.DROP_TABLE_DONE, tn);
  }
}

PrintMessages descQuery() : { String tn; }
{
  < DESC >
  tn = tableName()
  {
    if (!dbms.isExistingTable(tn)) {
      throw new ErrorException(Flags.NO_SUCH_TABLE);
    }
    dbms.printTable(tn);

    return new PrintMessages(Flags.DESC_DONE);
  }
}

PrintMessages insertQuery() : { String tn; }
{
  < INSERT >
  tn = tableName()
  insertColumnsAndSource()

  {
    return new PrintMessages(Flags.NOTHING);
  }
}

PrintMessages deleteQuery() : { String tn; }
{
  < DELETE >
  tn = tableName()
  (
    whereClause()
  )?
  
  {
    return new PrintMessages(Flags.NOTHING);
  }
}

PrintMessages selectQuery() : {}
{
  < SELECT >
  selectList()
  tableExpression()

  {
    return new PrintMessages(Flags.NOTHING);
  }
}

PrintMessages showTablesQuery() : {}
{
  < SHOW_TABLES >
  {
    dbms.showTables();
    return new PrintMessages(Flags.SHOW_TABLES_DONE);
  }
}

ArrayList<Column> tableElementList(Table t) : { Column c; ArrayList<Column> cl = new ArrayList<Column>(); }
{
  < LEFT_PAREN >
  c = tableElement(t, cl)
  {
    if (c != null) {
      cl.add(c);
    }
  }
  (
    < COMMA >
    c = tableElement(t, cl)
    {
      if (c!= null) {
        cl.add(c);
      }
    }
  )*
  < RIGHT_PAREN >
  {
    // column 중복 검사
    List<Column> l = cl;
    Set<Column> s = new HashSet<Column>();
    s.addAll(l);
    if (s.size() != l.size()) {
      throw new ErrorException(Flags.DUPLICATE_COLUMN_DEF_ERROR);
    }

    return cl;
  }
}

Column tableElement(Table t, ArrayList<Column> cl) : { Column c; int key; boolean pk = false; }
{
  c = columnDefinition()
  {
    return c;
  }
| key = tableConstraintDefinition(t, cl)
  {

    if (key == Flags.PRIMARY_KEY) {
      if (pk) {
        throw new ErrorException(Flags.DUPLICATE_PRIMARY_KEY_DEF_ERROR);
      }
      pk = true;
    }
    
    if (key == Flags.FOREIGN_KEY) {
    }
    return null;
  }
}

Column columnDefinition() : { String cn; DataType dt; boolean isNull = false; }
{
  cn = columnName()
  dt = dataType()
  (
    < NOT_NULL >
    { isNull = true; }
  )?
  {
    return new Column(cn, dt, isNull);
  }
}

void whereClause() : {}
{
  < WHERE >
  booleanValueExpression()
}

void fromClause() : {}
{
  < FROM >
  tableReferenceList()
}

void tableReferenceList() : {}
{
  referedTable()
  (
    < COMMA >
    referedTable()
  )?
}

void referedTable() : {}
{
  tableName()
  (
    LOOKAHEAD(4) // choice conflict 발생, LOOKAHEAD 사용
    < AS >
    tableName()
  )?
}

void booleanValueExpression() : {}
{
  booleanTerm()
  (
    < OR >
    booleanTerm()
  )*
}

void booleanTerm() : {}
{
  booleanFactor()
  (
    < AND >
    booleanFactor()
  )*
}

void booleanFactor() : {}
{
  (
    < NOT >
  )?
  booleanTest()
}

void booleanTest() : {}
{
  predicate()
| parenthesizedBooleanExpression()
}

void predicate() : {}
{
  LOOKAHEAD(4) // choice conflict 발생, LOOKAHEAD 사용
  comparisonPredicate()
| nullPredicate()
}

void comparisonPredicate() : {}
{
  compOperand()
  < COMP_OP >
  compOperand()
}

void compOperand() : {}
{
  comparableValue()
| (
    LOOKAHEAD(4) // choice conflict 발생, LOOKAHEAD 사용
    tableName()
    < PERIOD >
  )?
  columnName()
}

void parenthesizedBooleanExpression() : {}
{
  < LEFT_PAREN >
  booleanValueExpression()
  < RIGHT_PAREN >
}

void nullPredicate() : {}
{
  (
    LOOKAHEAD(4) // choice conflict 발생, LOOKAHEAD 사용
    tableName()
    < PERIOD >
  )?
  columnName()
  nullOperation()
}

void nullOperation(): {}
{
  < IS >
  (
    < NOT >
  )?
  < NULL >
}

int tableConstraintDefinition(Table t, ArrayList<Column> cl) : { ArrayList<String> cnl; }
{
  cnl = primaryKeyConstraint()
  {
    // 없는 column을 primary key로 지정할 경우
    String notExistingColumn = dbms.compareColumnLists(cnl, cl);
    if (notExistingColumn != null) {
      throw new ErrorException(Flags.NON_EXISTING_COLUMN_DEF_ERROR, notExistingColumn);
    }
    return Flags.PRIMARY_KEY;
  }
  | referentialConstraint(t)
  {
    return Flags.FOREIGN_KEY;
  }
}

void insertColumnsAndSource() : {}
{
  (
    columnNameList()
  )?
  valueList()
}

void valueList() : {}
{
  < VALUES >
  < LEFT_PAREN >
  value()
  (
    < COMMA >
    value()
  )*
  < RIGHT_PAREN >
}

void value() : {}
{
  < NULL >
| comparableValue()
}

void comparableValue() : {}
{
  < INT_VALUE >
| < CHAR_STRING >
| < DATE_VALUE >
}

void tableExpression() : {}
{
  fromClause()
  (
    whereClause()
  )?
}

void selectList() : {}
{
  < ALL >
| selectedColumn()
  (
    < COMMA >
    selectedColumn()
  )*
}

void selectedColumn() : {}
{
  (
    LOOKAHEAD(4) // choice conflict 발생, LOOKAHEAD 사용
    tableName()
    < PERIOD >
  )?
  columnName()
  (
    < AS >
    columnName()
  )?
}

ArrayList<String> primaryKeyConstraint() : { ArrayList<String> pkl;}
{
  < PRIMARY_KEY >
  pkl = columnNameList()
  {
    return pkl;
  }
}

void referentialConstraint(Table t) : { ArrayList<String> cnl; String rtn; ArrayList<String> fkl; ArrayList<Column> rcl = new ArrayList<Column>(); ArrayList<Column> cl = new ArrayList<Column>(); }
{
  < FOREIGN_KEY >
  cnl = columnNameList()
  < REFERENCES >
  rtn = tableName()
  fkl = columnNameList()
  {
    // foreign key가 존재하지 않는 table을 참조하는 경우
    if (!dbms.isExistingTable(rtn)) {
      throw new ErrorException(Flags.REFERENCE_TABLE_EXISTENCE_ERROR);
    }

    // foreign key가 참조하는 column의 개수가 다른 경우
    if (cnl.size() != fkl.size()) {
      throw new ErrorException(Flags.REFERENCE_TYPE_ERROR);
    }

    String notExistingColumn;
    
    // foreign key가 존재하지 않는 column을 참조하는 경우
    Table rt = dbms.getTable(rtn);
    notExistingColumn = dbms.compareColumnLists(fkl, rt.getColumns());
    if (notExistingColumn != null) {
      throw new ErrorException(Flags.REFERENCE_COLUMN_EXISTENCE_ERROR);
    }
    
    // 존재하지 않는 컬럼을 foreign key로 정의한 경우
    notExistingColumn = dbms.compareColumnLists(cnl, t.getColumns());
    if (notExistingColumn != null) {
      throw new ErrorException(Flags.NON_EXISTING_COLUMN_DEF_ERROR, notExistingColumn);
    }

    // 지목된 column들 추출
    for (Column rc: rt.getColumns()) {
      for (String fk: fkl) {
        if (fk.equals(rc.getName())) {
          rcl.add(rc);
        }
      }
    }
    for (Column c: t.getColumns()) {
      for (String cn: cnl) {
        if (cn.equals(c.getName())) {
          cl.add(c);
        }
      }
    }

    // primary key가 아닌 column을 참조하는 경우
    for (Column c: cl) {
      if (!c.getIsPrimaryKey()) {
        throw new ErrorException(Flags.REFERENCE_NON_PRIMARY_KEY_ERROR);
      }
    }

    // foreign key가 참조하는 column의 타입이 다른 경우
    boolean flag = true;
    for (Column rc: rcl) {
      for (Column c: cl) {
        if (c.getName().equals(rc.getName())) {
          if (c.getDataType() != rc.getDataType()) {
            flag = false;
            break;
          }
        }
      }
    }
    if (!flag) {
      throw new ErrorException(Flags.REFERENCE_TYPE_ERROR);
    }
  }
}

ArrayList<String> columnNameList() : { ArrayList<String> cl = new ArrayList<String>(); String cn; }
{
  < LEFT_PAREN >
  cn = columnName()
  {
    cl.add(cn);
  }
  (
    < COMMA >
    cn = columnName()
    {
      cl.add(cn);
    }
  )*
  < RIGHT_PAREN >
  {
    return cl;
  }
}

DataType dataType() : { int dt; int cl = 0; }
{
  < INT >
  {
    dt = Flags.INT;
    return new DataType(dt, cl);
  }
| (
    < CHAR >
    {
      dt = Flags.CHAR;
    }
    < LEFT_PAREN >
    < INT_VALUE >
    {
      cl = Integer.parseInt(token.image);
    }
    < RIGHT_PAREN >
    {
      if (cl <= 0) {
        throw new ErrorException(Flags.CHAR_LENGTH_ERROR);
      }
      return new DataType(dt, cl);
    }
  )
| < DATE >
  {
    dt = Flags.DATE;
    return new DataType(dt, cl);
  }
}

String tableName() : {}
{
  < LEGAL_IDENTIFIER >
  {
    return token.image.toLowerCase();
  }
}

String columnName() : {}
{
  < LEGAL_IDENTIFIER >
  {
    return token.image.toLowerCase();
  }
}